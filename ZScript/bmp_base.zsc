class BMP_Reader abstract
{
	static uint readInt(string data, double offset, double bytes = 4)
	{
		int x;
		if(bytes >= 1)
		{
			for(int i = 0; i < bytes; i++)
			{
				uint byte = data.ByteAt(offset + i);
				x |= byte << i*8;
			}
		}
		else if(bytes == 0.5)
		{
			uint byte = data.ByteAt(floor(offset));
			
			double frac = offset - floor(offset);
			if(frac == 0)
				x = byte >> 4;
			else
				x = byte & 0x0F;
		}
		return x;
	}
	
	static string GetBytes(string data, uint offset, uint bytes)
	{
		return data.Mid(offset, bytes);
	}
	
	static string GetFileData(string file)
	{
		int lump = Wads.CheckNumForFullName(file);
		return Wads.ReadLump(lump);
	}
	
	static BMP_Data ReadBMP(string bmpfile)
	{
		BMP_Data bmp = new("BMP_Data");
		string bmpdata = GetFileData(bmpfile);
		
		// Read header.
		string bmp_header = GetBytes(bmpdata, 0, 14);
		if(GetBytes(bmp_header, 0, 2) != "BM") 
		{
			console.printf("Invalid BMP header, file is corrupt or isn't a BMP!");
			bmp.Destroy();
			return NULL;
		}
		
		// Read DIB
		string bmp_info = GetBytes(bmpdata, 14, 40);
		double dib_size = readInt(bmp_info, 0);
		if(dib_size != 40) 
		{
			console.printf("BMP header isn't supported. (%d bytes)", dib_size);
			bmp.Destroy();
			return NULL;
		}
		
		// Get BMP info
		bmp.filesize = readInt(bmpdata, 2);
		bmp.width  = readInt(bmp_info, 4);
		bmp.height = readInt(bmp_info, 8);
		bmp.bpp = readInt(bmp_info, 14, 2);
		
		// Check compression
		uint compression = readInt(bmp_info, 16);
		if(compression != 0)
		{
			console.printf("BMP is compressed, currently only uncompressed BMPs are supported.");
			bmp.Destroy();
			return NULL;
		}
		
		// Get pixels
		bmp.colors = readInt(bmp_info, 32);
		if(!bmp.colors) bmp.isRGB = true;
		bmp.pixels.Resize(bmp.width * bmp.height);
		
		uint pixel_start  = readInt(bmpdata, 10);
		double pixelbytes = bmp.bpp / 8.; 
		uint pixels_size  = bmp.width * bmp.height * pixelbytes;
		string pixel_data = GetBytes(bmpdata, pixel_start, pixels_size);
		
		// Build palette
		if(!bmp.isRGB)
		{
			bmp.pal_pixels.Resize(bmp.width * bmp.height);
			bmp.palette.Resize(bmp.colors);
			uint pal_start = 54;
			uint pal_end   = readInt(bmpdata, 10);
			string pal = GetBytes(bmpdata, pal_start, pal_end-pal_start);
			for(int i = 0; i < bmp.colors; i++)
			{
				Color col = readInt(pal, (i*4));	
				bmp.palette[i] = col;
			}
		}
						
		// Resolve colors
		double offset = 0;
		for(int yy = 0; yy < bmp.height; yy++)
		{
			for(int xx = 0; xx < bmp.width; xx++)
			{
				Color col;
				uint pixel = readInt(pixel_data, offset, pixelbytes);
				if(bmp.isRGB) 
				{
					col = pixel;
					bmp.SetPixel(xx, (bmp.height - 1) - yy, col);
				}
				else
				{
					uint corrY = (bmp.height - 1) - yy;
					bmp.pal_pixels[(corrY * bmp.width) + xx] = pixel;
				}
				offset += pixelbytes;
			}
		}
		return bmp;
	}
}

class BMP_Data 
{
	bool isRGB;
	double filesize;
	uint bpp, colors;
	double width, height;
	Array<Color> palette;
	Array<uint> pal_pixels;
	Array<Color> pixels;
	
	void SetPixel(int x, int y, Color col)
	{
		if(isRGB)
		{
			pixels[(y * width) + x] = col;
		}
		else
		{
			console.printf("SetPixel doesn't work on paletted BMPs, use SetPixelIndex instead.");
		}
	}
	
	void AdjustPaletteColors(double rmax, double gmax, double bmax)
	{
		for(int c = 0; c < palette.Size(); c++)
		{
			Color col = palette[c];
			palette[c] = Color(int(col.r * rmax), int(col.g * gmax), int(col.b * bmax));
		}
	}
	
	void SetPixelIndex(int x, int y, uint pal_index)
	{
		pal_pixels[(y * width) + x] = pal_index;
	}
	
	void SetPaletteColor(uint index, Color col)
	{
		palette[index] = col;
	}
	
	Color GetPixel(int x, int y)
	{
		if(isRGB)
			return pixels[(y * width) + x];
		else
			return palette[pal_pixels[(y * width) + x]];
	}
}